import org.jetbrains.kotlin.gradle.dsl.ExplicitApiMode

plugins {
    id 'com.android.library'
    id 'org.jetbrains.kotlin.android'
    id 'org.jetbrains.kotlinx.kover'
    id 'org.jetbrains.kotlinx.binary-compatibility-validator'
}

ext {
    assertJVersion = '3.24.2'
    coroutinesVersion = '1.7.3'
    mockWebServerVersion = '4.12.0'
}

android {
    compileSdkVersion 33
    namespace = "com.parsely.parselyandroid"

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 33

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        testInstrumentationRunnerArguments clearPackageData: 'true'
    }
    testOptions {
        execution 'ANDROIDX_TEST_ORCHESTRATOR'
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    buildFeatures {
        buildConfig = false
    }
    packagingOptions {
        exclude 'META-INF/LGPL2.1'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/NOTICE'
    }

    publishing {
        singleVariant('release') {
            withSourcesJar()
            withJavadocJar()
        }
    }

    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }

    koverReport {
        filters {
            excludes {
                classes(
                        "*.BuildConfig"
                )
            }
        }
    }

    kotlin {
        explicitApi(ExplicitApiMode.Strict)
        jvmToolchain(17)
    }
}

dependencies {
    implementation 'androidx.appcompat:appcompat:1.4.2'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.13.3'
    implementation 'com.google.android.gms:play-services-ads-identifier:18.0.1'
    implementation 'androidx.lifecycle:lifecycle-process:2.6.2'
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutinesVersion"

    testImplementation 'org.robolectric:robolectric:4.10.3'
    testImplementation 'androidx.test:core:1.5.0'
    testImplementation "org.assertj:assertj-core:$assertJVersion"
    testImplementation 'junit:junit:4.13.2'
    testImplementation "com.squareup.okhttp3:mockwebserver:$mockWebServerVersion"
    testImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:$coroutinesVersion"

    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test:rules:1.5.0'
    androidTestImplementation "org.assertj:assertj-core:$assertJVersion"
    androidTestImplementation "com.squareup.okhttp3:mockwebserver:$mockWebServerVersion"
    androidTestImplementation 'androidx.test:runner:1.5.2'
    androidTestImplementation 'androidx.test.uiautomator:uiautomator:2.2.0'
    androidTestUtil 'androidx.test:orchestrator:1.4.2'
}

apply from: "${rootProject.projectDir}/publication.gradle"
